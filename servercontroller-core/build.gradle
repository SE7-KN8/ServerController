plugins {
    id 'edu.sc.seis.launch4j' version '2.3.0'
}

dependencies {
    compile project(':servercontroller-api')
    compile 'com.google.guava:guava:23.3-jre'
    compile 'org.jdom:jdom2:2.0.6'
    compile 'org.fxmisc.richtext:richtextfx:0.8.1'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.controlsfx:controlsfx:8.40.14'
    // For future use
    // compile 'io.github.lukehutch:fast-classpath-scanner:2.8.2'

    testCompile 'junit:junit:4.12'
}

def static getBuildNumber(){

    def number = System.getenv('BUILD_NUMBER')
    if(number == null){
        number = "snapshot"
    }

    return number
}

version = '0.5.14.' + getBuildNumber() + '_alpha'

jar {

    baseName = 'ServerController'

    manifest {
        attributes 'Version': project.version
        attributes 'Main-Class': 'sebe3012.servercontroller.ServerController'
        attributes 'Implementation-Title' : project.name
        attributes 'Implementation-Version' : project.version
    }

    //Standalone jar
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

launch4j {
    mainClassName = 'sebe3012.servercontroller.ServerController'
    icon = "$rootDir/raw/icon.ico"
    copyright = '2016-2017 by Sebastian Knackstedt'
    outfile = project.name + '-' + project.version + '.exe'
}