allprojects {
    task info doLast { task -> println "Task $task.project.name loaded"}
    task clearJarCopys doLast { delete "${projectDir}/jarCopys" }
}

def static getBuildNumber(){

    def number = System.getenv('TRAVIS_BUILD_NUMBER')
    if(number == null){
        number = "snapshot"
    }

    return number
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if (!it.name.startsWith("servercontroller-android-app")) {
        apply plugin: 'java'
        apply plugin: 'maven'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }

        uploadArchives {
            repositories {
                flatDir {
                    dirs 'versions'
                }
            }
        }
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

task copyJars(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$projectDir/jarCopys"
}

task copyAddons(type: Copy){
    from subprojects.collect { it.tasks.withType(Jar) }
    into System.getProperty("user.home") + "/.servercontroller/addons"
}